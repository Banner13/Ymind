[TOC]
###4 文件下载支持
	BitBake 的 fetch 模块是一个独立的库代码，它处理从远程系统下载源代码和文件的复杂性。获取源代码是构建软件的基石之一。因此，该模块构成了 BitBake 的重要组成部分。

	当前的 fetch 模块称为“fetch2”，指的是它是 API 的第二个主要版本。原始版本已过时，已从代码库中删除。因此，在所有情况下，“fetch”在本手册中都是指“fetch2”。

####4.1 下载（Fetch）
	BitBake 在获取源代码或文件时需要几个步骤。 fetcher 代码库按顺序处理两个不同的过程：从某个地方（缓存或其他方式）获取文件，然后将这些文件解压缩到特定位置，也许以特定方式。获取和解压缩文件通常可以选择在打补丁之后进行。但是，此模块不涉及修补。
	执行这个过程的第一部分的代码，一个 fetch，看起来像下面这样：
			src_uri = (d.getVar('SRC_URI') or "").split()
			fetcher = bb.fetch2.Fetch(src_uri, d)
			fetcher.download()
	此代码设置 fetch 类的实例。该实例使用来自 SRC_URI 变量的以空格分隔的 URL 列表，然后调用 download 方法来下载文件。
	fetch 类的实例化之后通常是：
			rootdir = l.getVar('WORKDIR')
			fetcher.unpack(rootdir)
	此代码将下载的文件解压缩到 WORKDIR 指定的文件。

			Note:
			为方便起见，这些示例中的命名与 OpenEmbedded 使用的变量相匹配。如果您想查看上面的代码，请检查 OpenEmbedded 类文件 base.bbclass 。

	SRC_URI 和 WORKDIR 变量没有硬编码到 fetcher 中，因为这些 fetcher 方法可以（并且被）用不同的变量名调用。例如，在 OpenEmbedded 中，共享状态 (sstate) 代码使用 fetch 模块来获取 sstate 文件。

	当调用 download() 方法时，BitBake 会尝试通过按特定搜索顺序查找源文件来解析 URL：
			- Pre-mirror Sites：BitBake 首先使用预镜像来尝试查找源文件。这些位置是使用 PREMIRRORS 变量定义的。
			- Source URI：如果预镜像失败，BitBake 使用原始 URL（例如来自 SRC_URI）。
			- Mirror Sites：如果发生获取失败，BitBake 接下来使用由 MIRRORS 变量定义的镜像位置。
	对于传递给 fetcher 的每个 URL，fetcher 调用处理该特定 URL 类型的子模块。当您为 SRC_URI 变量提供 URL 时，这种行为可能会造成一些混乱。考虑以下两个 URL：
			https://git.yoctoproject.org/git/poky;protocol=git
			git://git.yoctoproject.org/git/poky;protocol=http
	在前一种情况下，URL 被传递给 wget 提取器，它不理解“git”。因此，后一种情况是正确的形式，因为 Git 提取器确实知道如何使用 HTTP 作为传输。
	以下是一些显示常用镜像定义的示例：
			PREMIRRORS ?= "\
			   bzr://.*/.\*  http://somemirror.org/sources/ \
			   cvs://.*/.\*  http://somemirror.org/sources/ \
			   git://.*/.\*  http://somemirror.org/sources/ \
			   hg://.*/.\*   http://somemirror.org/sources/ \
			   osc://.*/.\*  http://somemirror.org/sources/ \
			   p4://.*/.\*   http://somemirror.org/sources/ \
			  svn://.*/.\*   http://somemirror.org/sources/"

			MIRRORS =+ "\
			   ftp://.*/.\*   http://somemirror.org/sources/ \
			   http://.*/.\*  http://somemirror.org/sources/ \
			   https://.*/.\* http://somemirror.org/sources/"
	请注意，BitBake 支持跨 URL。可以将 HTTP 服务器上的 Git 存储库镜像为 tarball。这就是前面示例中的 git:// 映射所做的。
	由于网络访问速度很慢，BitBake 会维护从网络下载的文件的缓存。任何非本地的源文件（即从 Internet 下载的）都放在下载目录中，该目录由 DL_DIR 变量指定。
	文件完整性对于复制构建至关重要。对于非本地存档下载，提取器代码可以验证 SHA-256 和 MD5 校验和，以确保存档已正确下载。您可以通过使用带有适当 varflags 的 SRC_URI 变量来指定这些校验和，如下所示：
			SRC_URI[md5sum] = "value"
			SRC_URI[sha256sum] = "value"
	您还可以将校验和指定为 SRC_URI 上的参数，如下所示：
			SRC_URI = "http://example.com/foobar.tar.bz2;md5sum=4a8e0f237e961fd7785d19d07fdb994d"
	如果存在多个 URI，您可以像前面的示例一样直接指定校验和，也可以命名 URL。以下语法显示了如何命名 URI：
			SRC_URI = "http://example.com/foobar.tar.bz2;name=foo"
			SRC_URI[foo.md5sum] = 4a8e0f237e961fd7785d19d07fdb994d
	下载文件并检查其校验和后，将在 DL_DIR 中放置一个“.done”标记。 BitBake 在后续构建期间使用此标记以避免再次下载或比较文件的校验和。

			Note:
			假定本地存储不受数据损坏的影响。如果不是这种情况，就会有更大的问题需要担心。

	如果设置了 BB_STRICT_CHECKSUM，任何没有校验和的下载都会触发错误消息。 BB_NO_NETWORK 变量可用于使任何尝试的网络访问都成为致命错误，这对于检查镜像是否完整以及其他事情很有用。

	如果 BB_CHECK_SSL_CERTS 设置为 0，则 SSL 证书检查将被禁用。此变量默认为 1，因此通常会检查 SSL 证书。

####4.2 拆包
	解压过程通常紧随下载之后。对于除 Git URL 之外的所有 URL，BitBake 使用通用的解包方法。

	您可以在 URL 中指定许多参数来控制解包阶段的行为：
		- unpack：控制是否对 URL 组件进行解包。如果设置为“1”（默认值），则解包组件。如果设置为“0”，解包阶段将单独保留文件。当您希望将存档复制进去而不是解压缩时，此参数很有用。
		- dos：适用于 .zip 和 .jar 文件，并指定是否对文本文件使用 DOS 行尾转换。
		- striplevel：在提取时从文件名中去除指定数量的前导组件（级别）
		- subdir：将特定 URL 解包到根目录中的指定子目录。

	unpack 调用会自动解压缩并解压缩带有“.Z”、“.z”、“.gz”、“.xz”、“.zip”、“.jar”、“.ipk”、“.rpm”的文件。 “.srpm”、“.deb”和“.bz2”扩展名以及tarball扩展名的各种组合。

	如前所述，Git fetcher 有自己的 unpack 方法，该方法已针对 Git 树进行了优化。基本上，此方法通过将树克隆到最终目录来工作。该过程使用引用完成，因此只需要一个 Git 元数据的中央副本。

####4.3 提取器
	如前所述，URL 前缀决定了 BitBake 使用哪个 fetcher 子模块。每个子模块可以支持不同的 URL 参数，这些参数将在以下部分中描述。

#####4.3.1 本地文件获取器（file://）
	该子模块处理以 file:// 开头的 URL。您在 URL 中指定的文件名可以是文件的绝对路径或相对路径。如果文件名是相对的，则 FILESPATH 变量的内容的使用方式与使用 PATH 查找可执行文件的方式相同。如果找不到该文件，则假定在调用 download() 方法时它在 DL_DIR 中可用。

	如果指定目录，则解压整个目录。

	以下是几个示例 URL，第一个相对 URL，第二个绝对 URL：
			SRC_URI = "file://relativefile.patch"
			SRC_URI = "file:///Users/ich/very_important_software"

#####4.3.2 HTTP/FTP wget 获取 (http://, ftp://, https://)
	此提取器从 Web 和 FTP 服务器获取文件。在内部，提取器使用 wget 实用程序。

	使用的可执行文件和参数由 FETCHCMD_wget 变量指定，默认为合理值。 fetcher 支持参数“downloadfilename”，允许指定下载文件的名称。在处理多个同名文件时，指定下载文件的名称有助于避免 DL_DIR 中的冲突。

	如果在 SRC_URI 中指定了用户名和密码，则基本授权标头将添加到每个请求，包括跨重定向。要将 Authorization 标头限制为第一个请求，请将“redirectauth=0”添加到参数列表中。

	一些示例 URL 如下所示：
			SRC_URI = "http://oe.handhelds.org/not_there.aac"
			SRC_URI = "ftp://oe.handhelds.org/not_there_as_well.aac"
			SRC_URI = "ftp://you@oe.handhelds.org/home/you/secret.plan"

			Note
			由于 URL 参数由分号分隔，因此在解析也包含分号的 URL 时会引入歧义，例如：
				SRC_URI = "http://abc123.org/git/?p=gcc/gcc.git;a=snapshot;h=a5dd47"
			应该通过用“&”字符替换分号来修改此类 URL：
				SRC_URI = "http://abc123.org/git/?p=gcc/gcc.git&a=snapshot&h=a5dd47"
			在大多数情况下，这应该有效。万维网联盟 (W3C) 建议对查询中的分号和“&”一视同仁。请注意，由于 URL 的性质，您可能还必须指定下载文件的名称：
				SRC_URI = "http://abc123.org/git/?p=gcc/gcc.git&a=snapshot&h=a5dd47;downloadfilename=myfile.bz2"

#####4.3.3 CVS 提取器 ((cvs://)
	该子模块处理从 CVS 版本控制系统中检出文件。您可以使用许多不同的变量对其进行配置：
		- FETCHCMD_cvs：运行 cvs 命令时使用的可执行文件的名称。这个名字通常是“cvs”。
		- SRCDATE：获取 CVS 源代码时使用的日期。 “now”的特殊值会导致每次构建时都更新结帐。
		- CVSDIR：指定临时结账的保存位置。该位置通常是 DL_DIR/cvs。
		- CVS_PROXY_HOST：用作 cvs 命令的“proxy=”参数的名称。
		- CVS_PROXY_PORT：用作 cvs 命令的“proxyport=”参数的端口号。

	除了标准的用户名和密码 URL 语法，您还可以使用各种 URL 参数配置 fetcher：

	支持的参数如下：
			- “method”：与 CVS 服务器通信的协议。默认情况下，此协议是“pserver”。如果“method”设置为“ext”，BitBake 会检查“rsh”参数并设置 CVS_RSH。您可以将“dir”用于本地目录。
			- “module”：指定要检出的模块。您必须提供此参数。
			- “tag”：描述应使用哪个 CVS TAG 进行结帐。默认情况下，TAG 为空。
			- “date”：指定日期。如果未指定“日期”，则配置的 SRCDATE 用于签出特定日期。 “now”的特殊值导致每次构建时都更新结帐。
			- “localdir”：用于重命名模块。实际上，您正在重命名模块解压缩到的输出目录。您正在强制模块进入相对于 CVSDIR 的特殊目录。
			- “rsh”：与“method”参数结合使用。
			- “scmdata”：当设置为“keep”时，使 CVS 元数据保存在 fetcher 创建的 tarball 中。压缩包被展开到工作目录中。默认情况下，CVS 元数据被删除。
			- “fullpath”：控制生成的检出是在模块级别（默认）还是在更深的路径。
			- “norecurse”：使 fetcher 只签出指定的目录，而不递归到任何子目录。
			- “port”：CVS 服务器连接的端口。

	一些示例 URL 如下所示：
			SRC_URI = "cvs://CVSROOT;module=mymodule;tag=some-version;method=ext"
			SRC_URI = "cvs://CVSROOT;module=mymodule;date=20060126;localdir=usethat"

#####4.3.4 子版本（SVN）提取器（svn://）
	这个 fetcher 子模块从 Subversion 源代码控制系统中获取代码。使用的可执行文件由 FETCHCMD_svn 指定，默认为“svn”。 fetcher 的临时工作目录由 SVNDIR 设置，通常为 DL_DIR/svn。
	支持的参数如下：
			- “module”：要签出的 svn 模块的名称。您必须提供此参数。您可以将此参数视为您想要的存储库数据的顶级目录。
			- “path_spec”：签出指定 svn 模块的特定目录。
			- “protocol”：要使用的协议，默认为“svn”。如果“protocol”设置为“svn+ssh”，也使用“ssh”参数。
			- “rev”：要签出的源代码的修订版本。
			- “scmdata”：设置为“keep”时，使“.svn”目录在编译时可用。默认情况下，这些目录被删除。
			- “ssh”：当“protocol”设置为“svn+ssh”时使用的可选参数。你可以使用这个参数来指定svn使用的ssh程序。
			- “transportuser”：需要时，设置传输的用户名。默认情况下，该参数为空。传输用户名不同于主 URL 中使用的用户名，后者被传递给 subversion 命令。

	以下是使用 svn 的三个示例：
			SRC_URI = "svn://myrepos/proj1;module=vip;protocol=http;rev=667"
			SRC_URI = "svn://myrepos/proj1;module=opie;protocol=svn+ssh"
			SRC_URI = "svn://myrepos/proj1;module=trunk;protocol=http;path_spec=${MY_DIR}/proj1"

#####4.3.5 Git 提取器 (git://)
	这个 fetcher 子模块从 Git 源代码控制系统中获取代码。提取器通过将远程的裸克隆创建到 GITDIR 中来工作，它通常是 DL_DIR/git2。然后，在检出特定树时，在解包阶段将此裸克隆克隆到工作目录中。这是通过使用替代品和参考来完成的，以最大限度地减少磁盘上的重复数据量并加快解包过程。可以使用 FETCHCMD_git 设置使用的可执行文件。
	此 fetcher 支持以下参数：
		- “protocol”：用于获取文件的协议。设置主机名时，默认值为“git”。如果未设置主机名，则 Git 协议为“file”。您还可以使用“http”、“https”、“ssh”和“rsync”。

					Note：
					当协议为“ssh”时，SRC_URI 中预期的 URL 与通常传递给 git clone 命令并由 Git 服务器提供以从中获取的 URL 不同。例如，通过 SSH 克隆时，GitLab 服务器为 mesa 返回的 URL 是 git@gitlab.freedesktop.org:mesa/mesa.git，但是 SRC_URI 中的预期 URL 如下：
						SRC_URI = "git://git@gitlab.freedesktop.org/mesa/mesa.git;branch=main;protocol=ssh;..."
					请注意：在项目路径之前为 / 更改了字符。

		- “nocheckout”：设置为“1”时，告诉 fetcher 在解包时不签出源代码。为有自定义例程检出代码的 URL 设置此选项。默认值为“0”。
		- “rebaseable”：表示上游 Git 存储库可以rebase。如果修订可以与分支分离，则应将此参数设置为“1”。在这种情况下，源镜像 tarball 是按修订完成的，这会降低效率。重新定位上游 Git 存储库可能会导致当前修订版从上游存储库中消失。此选项提醒 fetcher 小心保留本地缓存以备将来使用。此参数的默认值为“0”。
		- “nobranch”：告诉 fetcher 在设置为“1”时不检查分支的 SHA 验证。默认值为“0”。为引用对标记而不是分支有效的提交的配方设置此选项。
		- “bareclone”：告诉 fetcher 将一个裸克隆克隆到目标目录中，而不检查工作树。仅提供原始 Git 元数据。该参数也暗示了“nocheckout”参数。
		- “branch”：要克隆的 Git 树的分支。除非“nobranch”设置为“1”，否则这是一个强制参数。分支参数的数量必须与名称参数的数量相匹配。
		- “rev”：用于结帐的修订。默认为“主”。
		- “tag”：指定用于结帐的标签。要正确解析标签，BitBake 必须访问网络。因此，通常不使用标签。就 Git 而言，“tag”参数的行为实际上与“rev”参数相同。
		- “subpath”：将结帐限制为树的特定子路径。默认情况下，检出整个树。
		- “destsuffix”：放置结帐的路径的名称。默认情况下，路径是 git/。
		- “usehead”：允许本地 git:// URL 使用当前分支 HEAD 作为修订版，以与 AUTOREV 一起使用。 “usehead”参数意味着没有分支，并且仅在传输协议为 file:// 时才有效。

	以下是一些示例 URL：
			SRC_URI = "git://github.com/fronteed/icheck.git;protocol=https;branch=${PV};tag=${PV}"
			SRC_URI = "git://github.com/asciidoc/asciidoc-py;protocol=https;branch=main"
			SRC_URI = "git://git@gitlab.freedesktop.org/mesa/mesa.git;branch=main;protocol=ssh;..."

			Note:
			使用 git 作为软件主要源代码的 fetcher 时，应相应设置 S：
				S = "${WORKDIR}/git"

			Note:
			不支持直接在 git:// urls 中指定密码。有几个原因： SRC_URI 经常写到日志等地方，很容易泄露密码；在不删除密码的情况下共享元数据也很容易。 SSH 密钥、~/.netrc 和 ~/.ssh/config 文件可用作替代方案。

#####4.3.6 Git 子模块提取器（gitsm://）
	这个 fetcher 子模块继承自 Git fetcher，并通过获取存储库的子模块来扩展该 fetcher 的行为。 SRC_URI 被传递给 Git 提取器，如 Git Fetcher (git://) 部分中所述。

			Note:
			在“git://”和“gitsm://”URL 之间切换时，您必须清理配方。
			Git 子模块提取器不是完整的提取器实现。 fetcher 存在无法正确使用正常源镜像基础架构的已知问题。此外，它获取的子模块源对许可和源归档基础架构不可见。

#####4.3.7 ClearCase 提取器 (ccrc://)
	这个 fetcher 子模块从 ClearCase 存储库中获取代码。
	要使用此提取器，请确保您的配方具有正确的 SRC_URI、SRCREV 和 PV 设置。这是一个例子：
			SRC_URI = "ccrc://cc.example.org/ccrc;vob=/example_vob;module=/example_module"
			SRCREV = "EXAMPLE_CLEARCASE_TAG"
			PV = "${@d.getVar("SRCREV", False).replace("/", "+")}"
	提取器使用 rcleartool 或 cleartool 远程客户端，具体取决于哪个可用。
	以下是 SRC_URI 语句的选项：
		- vob：ClearCase VOB 的名称，必须包含前置“/”字符。此选项是必需的。
		- module：模块，必须包含前置“/”字符，在选定的 VOB 中。

				Note:
				module 和 vob 选项结合起来在视图配置规范中创建加载规则。例如，考虑本节开头 SRC_URI 语句中的 vob 和模块值。组合这些值会产生以下结果：
					load /example_vob/example_module

		- proto：协议，可以是http，也可以是https。
	默认情况下，提取器会创建一个配置规范。如果您希望将此规范写入默认值以外的区域，请使用配方中的 CCASE_CUSTOM_CONFIG_SPEC 变量来定义写入规范的位置。

			Nore:
			如果您指定此变量，SRCREV 将失去其功能。但是，SRCREV 仍然用于在获取后标记存档，即使它没有定义获取的内容。

	以下是一些值得一提的其他行为：
		- 使用 cleartool 时，cleartool 的登录由系统处理。登录不需要特殊步骤。
		- 为了对经过身份验证的用户使用 rcleartool，在使用 fetcher 之前需要“rcleartool 登录”。

#####4.3.8 Perforce 获取 (p4://)
	这个 fetcher 子模块从 Perforce 源代码控制系统中获取代码。使用的可执行文件由 FETCHCMD_p4 指定，默认为“p4”。 fetcher 的临时工作目录由 P4DIR 设置，默认为“DL_DIR/p4”。 fetcher 不使用 perforce 客户端，而是依靠 p4 文件来检索文件列表，并通过 p4 打印在本地传输这些文件的内容。
	要使用此提取器，请确保您的配方具有正确的 SRC_URI、SRCREV 和 PV 值。 p4 可执行文件能够使用系统的 P4CONFIG 环境变量定义的配置文件来定义 Perforce 服务器 URL 和端口、用户名和密码，如果您不希望将这些值保留在配方本身中。如果您选择不使用 P4CONFIG，或者显式设置 P4CONFIG 可以包含的变量，您可以指定 P4PORT 值，即服务器的 URL 和端口号，您可以直接在 SRC_URI 中的配方中指定用户名和密码。
	这是一个依赖 P4CONFIG 指定服务器 URL 和端口、用户名和密码并获取 Head Revision 的示例：
			SRC_URI = "p4://example-depot/main/source/..."
			SRCREV = "${AUTOREV}"
			PV = "p4-${SRCPV}"
			S = "${WORKDIR}/p4"
	这是一个示例，它指定服务器 URL 和端口、用户名和密码，并根据标签获取修订：
			P4PORT = "tcp:p4server.example.net:1666"
			SRC_URI = "p4://user:passwd@example-depot/main/source/..."
			SRCREV = "release-1.0"
			PV = "p4-${SRCPV}"
			S = "${WORKDIR}/p4"

			Note：
			您应该始终在您的配方中将 S 设置为“${WORKDIR}/p4”。

	默认情况下，提取器会从本地文件路径中删除软件仓库位置。在上面的例子中，example-depot/main/source/ 的内容会放在 ${WORKDIR}/p4 中。对于需要在本地保留部分远程仓库路径的情况，提取器支持两个参数：
		- “module”：
			要获取的顶级仓库位置或目录。此参数的值也可以指向软件仓库中的单个文件，在这种情况下，本地文件路径将包括模块路径。
		- “remotepath”：
			当与值“keep”一起使用时，提取器将在本地镜像指定位置的完整仓库路径，甚至与模块参数结合使用。
	下面是使用 module 参数的示例：
			SRC_URI = "p4://user:passwd@example-depot/main;module=source/..."
	在这种情况下，顶级目录 source/ 的内容将被提取到 ${P4DIR}，包括目录本身。顶级目录可在 ${P4DIR}/source/ 访问。
	这是 remotepath 参数的示例用法：
			SRC_URI = "p4://user:passwd@example-depot/main;module=source/...;remotepath=keep"
	在这种情况下，顶级目录 source/ 的内容将被提取到 ${P4DIR}，但完整的 depot 路径将在本地镜像。可以在 ${P4DIR}/example-depot/main/source/ 访问顶级目录。

####4.3.9 Repo 获取 (repo://)
	这个 fetcher 子模块从 google-repo 源代码控制系统中获取代码。提取器通过启动存储库的源并将其同步到 REPODIR 来工作，通常是 ${DL_DIR}/repo。

	此 fetcher 支持以下参数：
		- “protocol”：获取存储库清单的协议（默认值：git）。
		- “branch”：要获取的存储库的分支或标记（默认值：master）。
		- “manifest”：清单文件的名称（默认值：default.xml）。
	以下是一些示例 URL：
		SRC_URI = "repo://REPOROOT;protocol=git;branch=some_branch;manifest=my_manifest.xml"
		SRC_URI = "repo://REPOROOT;protocol=file;branch=some_branch;manifest=my_manifest.xml"

####4.3.10 Az 获取 (at://)
	此子模块从 Azure 存储帐户获取数据，它从 HTTP wget 获取器继承其功能，但修改其行为以适应对非公共数据使用共享访问签名 (SAS)。

	此类功能由变量设置：
		- AZ_SAS：Azure 存储共享访问签名提供对资源的安全委托访问，如果设置了此变量，Az Fetcher 将在从云中获取工件时使用它。

	您可以指定 AZ_SAS 变量，如下所示：
			AZ_SAS = "se=2021-01-01&sp=r&sv=2018-11-09&sr=c&skoid=<skoid>&sig=<signature>"
	这是一个示例网址：
			SRC_URI = "az://<azure-storage-account>.blob.core.windows.net/<foo_container>/<bar_file>"

#####4.3.11 箱子提取器 (crate://)
	这个子模块获取对应于 Rust 库和程序的 Rust 语言“板条箱”的代码以进行编译。这样的 crate 通常在 https://crates.io/ 上共享，但这个 fetcher 也支持其他 crate 注册表。
	SRC_URI 设置的格式必须是：
			SRC_URI = "crate://REGISTRY/NAME/VERSION"
	这是一个示例网址：
			SRC_URI = "crate://crates.io/glob/0.2.11"

#####4.3.12 其他提取器
	Fetch 子模块也存在于以下：
		- Bazaar (bzr://)
		- Mercurial (hg://)
		- npm (npm://)
		- OSC (osc://)
		- Secure FTP (sftp://)
		- Secure Shell (ssh://)
		- Trees using Git Annex (gitannex://)

	目前没有关于这些较少使用的 fetcher 子模块的文档。但是，您可能会发现代码有用且易读。

####4.4 自动修订
	我们需要在此处记录 AUTOREV 和 SRCREV_FORMAT。

####总结
	第四章提供了多种获取器。